struct Point2d:
    x: f32
    y: f32

    static func from_i_shift28(i: i32, shift: i32) -> Point2d:
        Point2d((29 - shift) as f32, (29 - i) as f32)

    func vector() -> Vector2d:
        Vector2d(this.x, this.y)

    func to(other: Point2d) -> Vector2d:
        Vector2d(other.x - this.x, other.y - this.y)

    func norm() -> f32:
        (this.x * this.x + this.y * this.y).sqrt()

    func dist(other: Point2d) -> f32:
        this.to(other).norm()

struct Vector2d:
    x: f32
    y: f32

    func point() -> Point2d:
        Point2d(this.x, this.y)

    func to(other: Vector2d) -> Vector2d:
        Vector2d(other.x - this.x, other.y - this.y)

    func norm() -> f32:
        (this.x * this.x + this.y * this.y).sqrt()

    func dot(other: Vector2d) -> f32:
        this.x * other.x + this.y * other.y

    func cross(other: Vector2d) -> f32:
        this.x * other.y - this.y * other.x

    func angle(is_branch_cut_positive: bool) -> f32:
        cos_value = (this.x/this.norm()).min(1.0)
        if cos_value + 1.0 < 0.001:
            if is_branch_cut_positive:
                180.0
            else:
                -180.0
        else:
            (this.y.sgnx() as f32) * cos_value.acos() * 180.0 / 3.1415926

    // rotation direction from this to other with minimal angle
    func rotation_direction_to(other: Vector2d) -> i32:
        // +1: counterclockwise
        // -1: clockwise
        // 0: no moving
        // expect not antiparallel
        cross = this.cross(other)
        if cross == 0.0:
            // expect nonzero and not antiparallel
            assert this.dot(other) > 0.0
            0
        else:
            cross.sgnx()

    func angle_to(other: Vector2d, is_branch_cut_positive: bool) -> f32:
        this_norm = this.norm()
        assert this_norm > 0.0
        other_norm = other.norm()
        assert other_norm > 0.0
        cos_value = (this.dot(other) / (this_norm * other_norm)).min(1.0)
        if cos_value + 1.0 < 0.001:
            if is_branch_cut_positive:
                180.0
            else:
                -180.0
        else:
            arc_angle = (this.rotation_direction_to(other) as f32) * cos_value.acos()
            arc_angle * 180.0 / 3.1415926